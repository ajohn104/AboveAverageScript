Program         ::= Stmt (Newline Stmt)* EOF
Block           ::= (Newline Stmt)*
Stmt            ::= DeclareStmt  
                 |  AssignStmt
                 |  NativeStmt
                 |  SwitchStmt
                 |  Loop
                 |  IfStmt
                 |  ConsumeStmt
                 |  ReturnStmt
                 |  ControlStmt
                 |  Exp

DeclareStmt     ::= 'let' (Id (',' Id)* '=' (ObjInd | ExpList)) | (SetEqual (',' Indent Newline SetEqual (',' Newline SetEqual)* Dedent ) )
AssignStmt      ::= (ExpList AssignOp (ObjInd | ExpList)) | (SetAssign (',' Indent Newline SetAssign (',' Newline SetAssign)* Dedent ) )
ConsumeStmt     ::= ExpList? '<-' Exp
ReturnStmt      ::= 'ret' Exp?
ControlStmt     ::= 'stop' | 'skip'
SetAssign       ::= Exp AssignOp Exp
SetEqual        ::= Id '=' Exp

// Beware uses of Exp with numbers > 0, since they don't save the current indent at the moment. Well, not in the way Exp does.
IfStmt          ::= 'if' Exp1 ':' Indent Block Dedent (Newline 'elif' Exp1 ':' Indent Block Dedent)* (Newline 'else' Indent Block Dedent)?
SwitchStmt      ::= 'switch' Exp1 ':' Indent Case+ Defaults? Dedent
Case            ::= Newline 'case' Exp18 ':' Indent Block Dedent                // I'm not actually sure what should be valid here.
Defaults        ::= Newline 'default' ':' Indent Block Dedent
NativeStmt      ::= '***native***'

Loop            ::= WhileLoop | ForLoop
WhileLoop       ::= DoWhile | While
DoWhile         ::= 'do' Indent Block Dedent Newline 'while' Exp1
While           ::= 'while' Exp1 ':' Indent Block Dedent
ForLoop         ::= (ForIn | ForColon | For) ':' Indent Block Dedent
ForIn           ::= 'for' Id (',' Id)? 'in' Exp17       // This might be worth double checking
ForColon        ::= 'for' Id ':' Exp17       // This might be worth double checking
For             ::= 'for' ( ('let'? Id '=')? Exp1 ',')? Exp1 ',' Exp1       // This might be worth double checking

Exp             ::= Exp1 (ForIn | ForColon)*
Exp1            ::= Exp2 ('if' Exp2 ('else' Exp2)?)?
Exp2            ::= Exp3 ('in' Exp3)*
Exp3            ::= Exp4 ('?' Exp4 ':' Exp4)?
Exp4            ::= Exp5 ('or' Exp5)*
Exp5            ::= Exp6 ('and' Exp6)*
Exp6            ::= Exp7 ('|' Exp7)*
Exp7            ::= Exp8 ('^' Exp8)*
Exp8            ::= Exp9 ('&' Exp9)*
Exp9            ::= Exp10 (EqualOp Exp10)*
Exp10           ::= Exp11 (CompareOp Exp11)*
Exp11           ::= Exp12 (ShiftOp Exp12)*
Exp12           ::= Exp13 (AddOp Exp13)*
Exp13           ::= Exp14 (MulOp Exp14)*
Exp14           ::= PrefixOp? Exp15
Exp15           ::= Exp16 PostfixOp?
Exp16           ::= 'new'? Exp17
Exp17           ::= Exp18 (ArrayCont | Call | '.' Exp18)*
Exp18           ::= Id | BoolLit | IntLit | StringLit | '(' Exp2 Newline? ')' | Func | ArrayLit | ObjectInline | This | RegExpLit

ArrayLit        ::= ('[' ']') | ArrayCont
ArrayCont       ::= '[' (Exp (',' Exp)*) | (Indent Newline Exp (',' Newline? Exp)* Dedent Newline) Newline? ']'
ExpList         ::= Exp (Newline? ',' Exp)*             // This'll require some thought before I try to make it happen correctly
RegExpLit       ::= '\/[^\/\\]+(?:\\.[^\/\\]*)*\/[igm]{0,3}'
Func            ::= 'func' (Id (',' Id)* )? '->' ('ret'? Exp1) | (Indent Block Dedent)
ObjectInline    ::= '{' (Property (',' Property)*) | (Indent Newline Property (',' Newline Property)* Dedent Newline) '}'
ObjInd          ::= Indent (Newline (Prop|PropInd) )+ Dedent
Prop            ::= (Id | BoolLit | StringLit) ':' Exp1
PropInd         ::= (Id | BoolLit | StringLit) ':' (Exp1 | ObjInd)
Call            ::= '(' ( ExpList (Newline? ',' Indent Newline Exp (Newline ',' Exp)* Dedent)? Newline?)? ')'    
// Using Exp1 here wouldn't have symmetry with the ExpList that comes before it. Consider it later.

AssignOp        ::= '=' | '+=' |'-=' | '*=' | '/=' | '%=' | '<<=' | '>>=' | '>>>=' | '&=' | '^=' | '|='
EqualOp         ::= '!==' | '===' | '!=' | '=='
CompareOp       ::= '>=' | '>' | '<=' | '<'
ShiftOp         ::= '>>>' | '>>' | '<<'
AddOp           ::= '+' | '-'
MulOp           ::= '%' | '/' | '*'
PrefixOp        ::= '--' | '++' | '-' | '+' | '~' | 'not'
PostfixOp       ::= '--' | '++'

IntLit          ::= '[+-]?((0x[a-fA-F0-9]+)|(\d+(\.\d+)?([eE][+-]?\d+)?))'
StringLit       ::= '\"[^\"\\]*(?:\\.[^\"\\]*)*\"|\'[^\'\\]*(?:\\.[^\'\\]*)*\''
Id              ::= '[_$a-zA-Z][$\w]*(?=[^$\w]|$)'
This            ::= '_'
Newline         ::= '\n'
Indent          ::= '\i'
Dedent          ::= '\d'
EOF             ::= '@EOF'